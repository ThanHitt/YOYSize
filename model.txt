
    model {
    for(i in 1:n){
      y[i] ~ dnorm(y.hat[i], tau.y[sid[i],yid[i]])
      y.hat[i] <- alpha + site.ran.mu[sid[i]] + b1*doy[i] + b2*yoyN[i] + b3*aduN[i] + b4*fp[i] + 
      b5*ft[i] + b6*wp[i] + b7*wt[i] + b8*sp[i] + b9*st[i]
    }
    
    for(j in 1:J){
      # Random site effect on mean [j=sites]
      site.ran.mu[j] ~ dnorm(site.hat[j], tau.site.mu)
      site.hat[j] <- b10*elev[j] + b11*uba[j] + b12*grad[j]

      # Random site effect on SD [j=sites]
      site.ran.sigma[j] ~ dnorm(site.ran.sig.mu[j], tau.site.sigma)
      site.ran.sig.mu[j] <- b22*elev[j] + b23*uba[j] + b24*grad[j]

      # Random site SD [j=sites, m=years]
      for(m in 1:M){
        tau.y[j,m] <- pow(sigma[j,m], -2)
        log(sigma[j,m]) <- log.sigma[j,m]
        log.sigma[j,m] ~ dnorm(mu.sigma[j,m], inv.omega.sigma.squared)
        mu.sigma[j,m] <- Mu.Sigma + site.ran.sigma[j] + b13*doy1[j,m] + b14*yoyN1[j,m] + b15*aduN1[j,m] + b16*fp1[j,m] + 
                         b17*ft1[j,m] + b18*wp1[j,m] + b19*wt1[j,m] + b20*sp1[j,m] + b21*st1[j,m]
      }
    }

    # Priors
    b1 ~ dnorm(0, 0.001)
    b2 ~ dnorm(0, 0.001)
    b3 ~ dnorm(0, 0.001)
    b4 ~ dnorm(0, 0.001)
    b5 ~ dnorm(0, 0.001)
    b6 ~ dnorm(0, 0.001)
    b7 ~ dnorm(0, 0.001)
    b8 ~ dnorm(0, 0.001)
    b9 ~ dnorm(0, 0.001)
    b10 ~ dnorm(0, 0.001)
    b11 ~ dnorm(0, 0.001)
    b12 ~ dnorm(0, 0.001)
    b13 ~ dnorm(0, 0.001)
    b14 ~ dnorm(0, 0.001)
    b15 ~ dnorm(0, 0.001)
    b16 ~ dnorm(0, 0.001)
    b17 ~ dnorm(0, 0.001)
    b18 ~ dnorm(0, 0.001)
    b19 ~ dnorm(0, 0.001)
    b20 ~ dnorm(0, 0.001)
    b21 ~ dnorm(0, 0.001)
    b22 ~ dnorm(0, 0.001)
    b23 ~ dnorm(0, 0.001)
    b24 ~ dnorm(0, 0.001)
    alpha ~ dnorm(0, 0.001)
    tau.site.mu <- pow(sig.site.mu, -2)
    sig.site.mu ~ dunif(0, 20)
    Mu.Sigma ~ dnorm(0, 0.001)
    Med.Sigma <- exp(Mu.Sigma)
    inv.omega.sigma.squared <- pow(omega.sigma, -2)
    omega.sigma ~ dunif(0, 20)
    tau.site.sigma <- pow(sig.site.sigma, -2)
    sig.site.sigma ~ dunif(0, 20)
    } # end model
    
